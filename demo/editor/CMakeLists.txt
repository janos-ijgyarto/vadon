MESSAGE("Configuring Vadon Editor Demo")

# FIXME: a lot of this logic is the same as the engine targets
# Find some way to deduplicate?
set(VADONDEMO_CURRENT_TARGET "VadonDemoEditor")

add_executable(${VADONDEMO_CURRENT_TARGET})

add_subdirectory(VadonDemo)

get_property("VADONEDITORDEMO_SOURCES" TARGET ${VADONDEMO_CURRENT_TARGET} PROPERTY SOURCES)
source_group(TREE ${CMAKE_CURRENT_SOURCE_DIR} FILES ${VADONEDITORDEMO_SOURCES})

target_include_directories(${VADONDEMO_CURRENT_TARGET} PRIVATE ${CMAKE_CURRENT_SOURCE_DIR})

set(VADONEDITORDEMO_LIBRARIES VadonEditor VadonDemoCommon)
target_link_libraries(${VADONDEMO_CURRENT_TARGET} PRIVATE ${VADONEDITORDEMO_LIBRARIES})
	
# Enable console window for debug builds, disable in release
if(WIN32)
    target_link_options(${VADONDEMO_CURRENT_TARGET} PRIVATE "$<IF:$<CONFIG:Debug>,/SUBSYSTEM:CONSOLE,/SUBSYSTEM:WINDOWS>" "/ENTRY:mainCRTStartup")
else()
	# Enable console window for debug builds, disable in release
    target_link_options(${VADONDEMO_CURRENT_TARGET} PRIVATE "$<IF:$<NOT:$<CONFIG:Debug>>,-mwindows,>")
endif()

set_target_properties(${VADONDEMO_CURRENT_TARGET} PROPERTIES FOLDER ${VADON_PARENT_FOLDER}${PROJECT_NAME})

set_target_properties(${VADONDEMO_CURRENT_TARGET}
    PROPERTIES
    RUNTIME_OUTPUT_DIRECTORY "${CMAKE_BINARY_DIR}/bin"
)

# Create install target (this can assemble all the required executables, DLLs, etc.)
set(VADONDEMO_INSTALL_RUNTIME_PREFIX "Editor")
set(VADONDEMO_INSTALL_EDITOR_COMPONENT "VadonDemoEditor")

install(TARGETS ${VADONDEMO_CURRENT_TARGET} ${VADONDEMO_RUNTIME_DLLS} RUNTIME COMPONENT ${VADONDEMO_INSTALL_EDITOR_COMPONENT} DESTINATION ${VADONDEMO_INSTALL_RUNTIME_PREFIX}/${VADON_INSTALL_RUNTIME_DESTINATION})
install(FILES $<TARGET_RUNTIME_DLLS:${VADONDEMO_CURRENT_TARGET}> COMPONENT ${VADONDEMO_INSTALL_EDITOR_COMPONENT} DESTINATION ${VADONDEMO_INSTALL_RUNTIME_PREFIX}/${VADON_INSTALL_RUNTIME_DESTINATION})